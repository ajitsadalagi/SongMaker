plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.songmaker.app'
    compileSdk 34

    signingConfigs {
        release {
            def propsFile = rootProject.file('signing.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile file(props['storeFile'] ?: '/Users/ajit/makesong-release-key.keystore')
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "com.songmaker.app"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            def propsFile = rootProject.file('signing.properties')
            if (propsFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    // Exclude large LLM models from assets so compressDebugAssets does not run out of memory.
    // Copy the .tflite to the device at: Android/data/com.songmaker.app/files/models/
    sourceSets {
        main {
            assets {
                exclude 'model/**'
                exclude '**/*.tflite'
                exclude '**/*.task'
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    // On-device LLM for lyric generation (Gemma 2B etc.)
    implementation 'com.google.mediapipe:tasks-genai:0.10.32'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

// Suppress "source value 8 is obsolete" javac warnings
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += '-Xlint:-options'
}
